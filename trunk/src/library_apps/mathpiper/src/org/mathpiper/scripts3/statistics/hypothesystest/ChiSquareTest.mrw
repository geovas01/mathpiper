%mathpiper,def="ChiSquareTest"

/*
   Hypothesys testing routines
   Andrei Zorine,2002 <zoav1@uic.nnov.ru>
*/

/* Stub: ChiSquare's CDF is computed as IncompleteGamma(x,dof/2)/Gamma(dof/2); */

100 # ChiSquareTest( observed'freqs_IsList, expected'freqs_IsList, estimated'params_IsInteger)
  <--
 [
   Local( nominator, chi2, p'value, k, dof);
   k:=Length(observed'freqs);
   nominator:=(observed'freqs-expected'freqs)^2; //threading
   chi2:=Sum(i,1,k,nominator[i]/(expected'freqs[i]));
   dof := k-estimated'params-1; // degrees of freedom
   p'value:=1-N(IncompleteGamma(chi2/2,dof/2)/Gamma(dof/2));
   { TestStatistics <- chi2 , P'value <- p'value,Atom("dof") <- dof};

 ];


100 # ChiSquareTest( observed'freqs_IsList,
	 expected'freqs_IsList) <-- ChiSquareTest( observed'freqs, expected'freqs, 0);


%/mathpiper



%mathpiper_docs,name="ChiSquareTest",categories="User Functions;Statistics & Probability"
*CMD ChiSquareTest --- Pearson's ChiSquare test
*STD

*CALL
	ChiSquareTest(observed,expected)
	ChiSquareTest(observed,expected,params)

*PARMS
{observed} -- list of observed frequencies

{expected} -- list of expected frequencies

{params} -- number of estimated parameters

*DESC
{ChiSquareTest} is intended to find out if our sample was drawn from a
given distribution or not. To find this out, one has to calculate
observed frequencies into certain intervals and expected ones. To
calculate expected frequency the formula $n[i]:=n*p[i]$ must be used,
where $p[i]$ is the probability measure of $i$-th interval, and $n$ is
the total number of observations. If any of the parameters of the
distribution were estimated, this number is given as
{params}.

The function returns a list of three local substitution rules. First
of them contains the test statistic, the second contains the value of the parameters, and
the last one contains the degrees of freedom. 

The test statistic is distributed as ChiSquareDistribution.
%/mathpiper_docs