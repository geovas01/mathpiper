%mathpiper,def="CumulativeDistributionFunction"

/* Evaluates distribution dst at point x
   known distributions are:
   1. Discrete distributions
   -- BernoulliDistribution(p)
   -- BinomialDistribution(p,n)
   -- DiscreteUniformDistribution(a,b)
   -- PoissonDistribution(l)
   -- HypergeometricDistribution(N, M)
   2. Continuous distributions
   -- ExponentialDistribution(l)
   -- NormalDistrobution(a,s)
   -- ContinuousUniformDistribution(a,b)
   -- tDistribution(m)
   -- GammaDistribution(m)
   -- ChiSquareDistribution(m)

  DiscreteDistribution(domain,probabilities) represent arbitrary
  distribution with finite number of possible values; domain list
  contains possible values such that
  Pr(X=domain[i])=probabilities[i].
  TODO: Should domain contain numbers only?
*/


/* Evaluates Cumulative probability function CumulativeDistributionFunction(x)=Pr(X<x) */

Retract("CumulativeDistributionFunction", *);

10 # CumulativeDistributionFunction(BernoulliDistribution(_p), x_IsNumber) <-- If(x<=0,0,If(x>0 And x<=1, p,1));
11 # CumulativeDistributionFunction(BernoulliDistribution(_p), _x) <-- Hold(If(x<=0,0,If(x>0 And x<=1, p,1)));

10 # CumulativeDistributionFunction(BinomialDistribution(_p,_n),m_IsNumber)_(m<0) <-- 0;
10 # CumulativeDistributionFunction(BinomialDistribution(_p,n_IsInteger),m_IsNumber)_(m>n) <-- 1;
10 # CumulativeDistributionFunction(BinomialDistribution(_p,_n),_m) <-- Sum @ { i, 0, Floor(m), ProbabilityDensityFunction(BinomialDistribution(p,n),i)};

10 # CumulativeDistributionFunction(DiscreteUniformDistribution( a_IsNumber, b_IsNumber), x_IsNumber)_(x<=a) <-- 0;
10 # CumulativeDistributionFunction(DiscreteUniformDistribution( a_IsNumber, b_IsNumber), x_IsNumber)_(x>b) <-- 1;
10 # CumulativeDistributionFunction(DiscreteUniformDistribution( a_IsNumber, b_IsNumber), x_IsNumber)_(a<x And x<=b) <-- (x-a)/(b-a+1);
11 # CumulativeDistributionFunction(DiscreteUniformDistribution( _a, _b), _x) <--Hold(If(x<=a,0,If(x<=b,(x-a)/(b-a),1)));

10 # CumulativeDistributionFunction(PoissonDistribution(_l), x_IsNumber)_(x<=0) <-- 0;
10 # CumulativeDistributionFunction(PoissonDistribution(_l), _x) <-- Sum @ {i,0,x-1,ProbabilityDensityFunction(PoissonDistribution(l),i)};

10 # CumulativeDistributionFunction(ChiSquareDistribution(_m), _x) <-- IncompleteGamma(x/2,m/2)/Gamma(x/2);
10 # CumulativeDistributionFunction(DiscreteDistribution( dom_IsList, prob_IsList), _x)   <--
      [
	 Local(i,cdf,y);

	 i := 1;
	 cdf:=0;
	 y:=dom[i];
	 While(y<x) [cdf:=cdf+prob[i];i++;];
	 cdf;
      ];

10 # CumulativeDistributionFunction(HypergeometricDistribution( N_IsNumber, M_IsNumber, n_IsNumber), x_IsNumber)_(M <= N And n <= N) <-- 
[
    Sum @ {i,0,x,ProbabilityDensityFunction(HypergeometricDistribution(N, M, n),i)};
];

%/mathpiper

    %output,preserve="false"
      Result: True
.   %/output




%mathpiper_docs,name="CumulativeDistributionFunction",categories="User Functions;Statistics & Probability"
*CMD CumulativeDistributionFunction --- cumulative distribution function
*STD
*CALL
	CumulativeDistributionFunction(dist,x)

*PARMS
{dist} -- a distribution type

{x} -- a value of random variable


*SEE ProbabilityDensityFunction, Expectation
%/mathpiper_docs