%mathpiper,def="xContent"

/*------------------------------------------------------------------------
 *  Finds the Content of a univariate or multivariate polynomial
 *------------------------------------------------------------------------*/

Retract("xContent",*);
Retract("GetContent",*);
Retract("xPeel",*);
Retract("FinalPeel",*);

10 # xContent( expr_CanBeUni ) <--
[
    Local(vars,L,cont,result);
    vars   := VarList(expr);
    L      := Length(vars);
    result := GetContent(expr,L);    
    resid  := Simplify(expr/result);
    cont   := result * FinalPeel(resid);
];


10 # GetContent(_expr,_L)_(L=0) <-- expr;


12 # GetContent(_expr,_L)_(L=1) <--
[
    Local(uni,result);
    uni    := MakeUni(expr);
    result := NormalForm(Gcd(uni[3])*uni[1]^uni[2]);
];


14 # GetContent(_expr,_L) <--
[
    Local(cont2,lst);
    cont2  := MultiSimp(expr);
    lst    := Listify(cont2);
    result := xPeel(lst);

];

UnFence("GetContent",2);


10 # xPeel(L_IsList)_(L[1] = /) <--
[
    Local(result);
    result := L[3];
    result := xPeel(Listify(L[2])) / result;
];


10 # xPeel(L_IsList)_(L[1] = *) <--
[
    Local(result);
    result := L[2];
    result := xPeel(Listify(L[3])) * result;
    result;
];


12 # xPeel(L_IsList) <-- 1;


10 # FinalPeel(_poly) <--
[
    Local(uni,Lu3,v,n,fac,fac2,result,p1);
    uni := MakeUni(poly);
    Lu3 := Length(uni[3]);
    v := uni[1];
    n := uni[2];
    fac := If( n > 0, v^n, 1 );
    result := fac;
    p1 := uni[3][Lu3];
    While ( Type(p1) = "UniVariate" )
    [
        v := p1[1];
        n := p1[2];
        fac2 := If( n > 0, v^n, 1 );
        If( fac2 != 1, result := result * fac2 );
        p1 := p1[3][1];
   ];
   result;
];

%/mathpiper

    %output,preserve="false"
      Result: True
.   %/output




%mathpiper_docs,name="xContent",categories="User Functions;Factors"
*CMD Content --- content of a polynomial
*STD
*CALL
	Content(expr)

*PARMS

{expr} -- a univariate or multivariate polynomial

*DESC

This is an experimental version of the existing function Contact(), 
with extended features.  It is provided primarily for testing purposes,
until it is ready to replace the older version.

This command determines the {content} of a polynomial. 
The {content} is the greatest common divisor of all the terms in the
polynomial. 

For a {univariate} polynomial, the {content} will consist of a number
or the product of a number and the lowest power of the variable (if not 0). 

For a {multivariate} polynomial, the {content} will consist of a number
or the product of a number and the lowest power of each variable present
in all terms.

Every polynomial can be written as the product of its {content}
and its {primitive part} (q.v.). This representation is usually
the first step in any attempt to factor the polynomial.

*E.G.

In> poly2:=2*a*x^2+4*a*x
Result: 2*a*x^2+4*a*x

In> cx:=xContent(poly2)
Result: 2*a*x

In> ppx := xPrimitivePart(poly2)
Result: x+2

In> Expand(ppx*cx)
Result: 2*a*x^2+4*a*x

*SEE Content, PrimitivePart, Gcd, xPrimitivePart
%/mathpiper_docs

    %output,preserve="false"
      
.   %/output




%mathpiper
//========================   TESTING    =============================

pqs := {};
Push(pqs,2);
Push(pqs,2*x);
Push(pqs,2*x-1);
Push(pqs,2*x-4);
Push(pqs,2*a*x-4*a);
Push(pqs,2*a*x^2-4*a);
Push(pqs,2*a*x^2-4*a*x+4*a);
Push(pqs,2*a*x^2-4*x+4*a);
Push(pqs,2*a*x^2-4*x+4*a+4);
Push(pqs,3*a*x^3-6*a*x^2+3*a*x);
Push(pqs,6*a*n^4*m^3+24*a*n^3*m^2-18*a*n^2*m);

pqs := Reverse(pqs);

ForEach(pq,pqs)
[
    T := Time() ( content := (xContent(pq)) );
    Tell("  ",T);
    Tell("  ",pq);
    Tell("  ",content);
    NewLine();
];

%/mathpiper

    %output,preserve="false"
      Result: True
      
      Side Effects:
      <<   >> T : 0.236864970 
      <<   >> pq : 2 
      <<   >> content : 2 
      
      <<   >> T : 0.028398693 
      <<   >> pq : 2*x 
      <<   >> content : 2*x 
      
      <<   >> T : 0.085265647 
      <<   >> pq : 2*x-1 
      <<   >> content : 1 
      
      <<   >> T : 0.070969786 
      <<   >> pq : 2*x-4 
      <<   >> content : 2 
      
      <<   >> T : 0.252829008 
      <<   >> pq : 2*a*x-4*a 
      <<   >> content : 2*a 
      
      <<   >> T : 0.269286884 
      <<   >> pq : 2*a*x^2-4*a 
      <<   >> content : 2*a 
      
      <<   >> T : 0.446782389 
      <<   >> pq : 2*a*x^2-4*a*x+4*a 
      <<   >> content : 2*a 
      
      <<   >> T : 0.355171789 
      <<   >> pq : 2*a*x^2-4*x+4*a 
      <<   >> content : 2 
      
      <<   >> T : 0.365234253 
      <<   >> pq : 2*a*x^2-4*x+4*a+4 
      <<   >> content : 2 
      
      <<   >> T : 0.440929806 
      <<   >> pq : 3*a*x^3-6*a*x^2+3*a*x 
      <<   >> content : 3*a*x 
      
      <<   >> T : 0.779863203 
      <<   >> pq : 6*a*n^4*m^3+24*a*n^3*m^2-18*a*n^2*m 
      <<   >> content : 6*a*n^2*m 
      
      
.   %/output


