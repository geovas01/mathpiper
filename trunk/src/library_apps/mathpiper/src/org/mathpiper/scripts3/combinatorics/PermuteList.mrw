%mathpiper,def="PermuteList"

Function("PermuteList",{result,list})
[
  If (Length(list) = 0,
  [
    result;
  ],
  [
    Local(head);
    Local(newresult);
    Local(i);
    head:=list[1];
    newresult:={};
    ForEach(item,result)
    [
      For(i:=Length(item)+1,i>0,i--)
      [
        DestructiveInsert(newresult,1,Insert(item,i,head));
      ];
    ];
    newresult:=DestructiveReverse(newresult);
    PermuteList(newresult,Rest(list));
  ]);
];


Function("PermuteList",{list})
[
  PermuteList({{}},list);
];

%/mathpiper



%mathpiper_docs,name="PermuteList",categories="User Functions;Combinatorics"
*CMD PermuteList --- get all permutations of a list
*STD
*CALL
	PermuteList(list)

*PARMS

{list} -- a list of elements

*DESC

PermuteList returns a list with all the permutations of
the original list.

*E.G.

	In> PermuteList({a,b,c})
	Out> {{a,b,c},{a,c,b},{c,a,b},{b,a,c},
	{b,c,a},{c,b,a}};

*SEE LeviCivita
%/mathpiper_docs
