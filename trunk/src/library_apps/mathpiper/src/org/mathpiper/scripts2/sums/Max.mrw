%mathpiper,def="Max"

/*  this is disabled because some functions seem to implicitly define Min / Max with a different number of args, and then MathPiper is confused if it hasn't loaded all the Function() declarations beforehand.
FIXME
/// Min, Max with many arguments
*/

Retract("Max", 1);
Retract("Max", 2);
Retract("Max", 3);

//Function() Max(list);

//Function() Max(l1, l2);

Function() Max(l1, l2, l3, ...);



10 # Max(_l1, _l2, l3_IsList) <-- Max(Concat({l1, l2}, l3));
20 # Max(_l1, _l2, _l3) <-- Max({l1, l2, l3});
/**/

10 # Max(l1_IsList,l2_IsList) <-- Map("Max",{l1,l2});


20 # Max(l1_IsRationalOrNumber,l2_IsRationalOrNumber) <-- If(l1>l2,l1,l2);


30 # Max(l1_IsConstant,l2_IsConstant) <-- If(N(Eval(l1-l2))>0,l1,l2);

// Max on empty lists
10 # Max({}) <-- Undefined;


20 # Max(list_IsList) <--
[
  Local(result);
  result:= list[1];
  ForEach(item,Tail(list)) result:=Max(result,item);
  result;
];


30 # Max(_x) <-- x;

%/mathpiper



%mathpiper_docs,name="Max"
*CMD Max --- maximum of a number of values
*STD
*CALL
	Max(x,y)
	Max(list)

*PARMS

{x}, {y} -- pair of values to determine the maximum of

{list} -- list of values from which the maximum is sought

*DESC

This function returns the maximum value of its argument(s). If the
first calling sequence is used, the larger of "x" and "y" is
returned. If one uses the second form, the largest of the entries in
"list" is returned. In both cases, this function can only be used
with numerical values and not with symbolic arguments.

*E.G.

	In> Max(2,3);
	Out> 3;
	In> Max({5,8,4});
	Out> 8;

*SEE Min, Sum
%/mathpiper_docs