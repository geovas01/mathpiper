%mathpiper,def="true"

 // (expression, level=1..3

// Some shortcuts to match prev interface

10 # LogicSimplify(_proposition, _level)_(level<2)  <-- CNF(proposition);

20 # LogicSimplify(_proposition, _level) <--
[
  Local(cnf, list, clauses);
  Check(level > 1, "Wrong level");
  // First get the CNF version of the proposition
  Set(cnf, CNF(proposition));

  If(level <= 1, cnf, [
    Set(list, Flatten(cnf, "And"));
    Set(clauses, {});
    ForEach(clause, list)
    [
      Local(newclause);
      //newclause := BubbleSort(LogicRemoveTautologies(Flatten(clause, "Or")), LessThan);
      Set(newclause, LogicRemoveTautologies(Flatten(clause, "Or")));
      If(newclause != {True}, DestructiveAppend(clauses, newclause));
    ];

    /*
        Note that we sort each of the clauses so that they look the same,
        i.e. if we have (A And B) And ( B And A), only the first one will
        persist.
    */
    Set(clauses, RemoveDuplicates(clauses));

    If(Equals(level, 3) And (Length(clauses) != 0), [
        Set(clauses, DoUnitSubsumptionAndResolution(clauses));
        Set(clauses, LogicCombine(clauses));
    ]);

    Set(clauses, RemoveDuplicates(clauses));

    If(Equals(Length(clauses), 0), True, [
        /* assemble the result back into a boolean expression */
        Local(result);
        Set(result, True);
        ForEach(item,clauses)
        [
            Set(result, result And UnFlatten(item, "Or", False));
        ];

        result;
    ]);
  ]);
];

%/mathpiper