%mathpiper,def="ProbabilityDensityFunction"

/* Evaluates distribution dst at point x
   known distributions are:
   1. Discrete distributions
   -- BernoulliDistribution(p)
   -- BinomialDistribution(p,n)
   -- DiscreteUniformDistribution(a,b)
   -- PoissonDistribution(l)
   2. Continuous distributions
   -- ExponentialDistribution(l)
   -- NormalDistrobution(a,s)
   -- ContinuousUniformDistribution(a,b)
   -- tDistribution(m)
   -- GammaDistribution(m)
   -- ChiSquareDistribution(m)

  DiscreteDistribution(domain,probabilities) represent arbitrary
  distribution with finite number of possible values; domain list
  contains possible values such that
  Pr(X=domain[i])=probabilities[i].
  TODO: Should domain contain numbers only?
*/

10 # ProbabilityDensityFunction(BernoulliDistribution(_p),0) <-- p;
10 # ProbabilityDensityFunction(BernoulliDistribution(_p),1) <-- 1-p;
10 # ProbabilityDensityFunction(BernoulliDistribution(_p),x_IsNumber)_(x != 0 And x != 1) <-- 0;
10 # ProbabilityDensityFunction(BernoulliDistribution(_p),_x) <-- Hold(If(x=0,p,If(x=1,1-p,0)));

10 # ProbabilityDensityFunction(BinomialDistribution(_p,_n),_k) <-- Bin(n,k)*p^k*(1-p)^(n-k);

10 # ProbabilityDensityFunction(DiscreteUniformDistribution(_a,_b), x_IsNumber) <-- If(x<a Or x>b, 0 ,1/(b-a+1));
11 # ProbabilityDensityFunction(DiscreteUniformDistribution(_a,_b), _x) <-- Hold(If(x<a Or x>b, 0 ,1/(b-a+1)));

10 # ProbabilityDensityFunction(PoissonDistribution(_l), n_IsNumber) <-- If(n<0,0,Exp(-l)*l^n/n!);
11 # ProbabilityDensityFunction(PoissonDistribution(_l),_n) <-- Exp(-l)*l^n/n!;

10 # ProbabilityDensityFunction(GeometricDistribution(_p),_n) <--If(n<0,0,p*(1-p)^n);

10 # ProbabilityDensityFunction(ExponentialDistribution(_l), _x) <-- If(x<0,0,l*Exp(-l*x));

10 # ProbabilityDensityFunction(NormalDistribution(_m,_s),_x) <-- Exp(-(x-m)^2/(2*s))/Sqrt(2*Pi*s);

10 # ProbabilityDensityFunction(ContinuousUniformDistribution(_a,_b),x)_(a<b) <-- If(x<a Or x>b,0,1/(b-a));

10 # ProbabilityDensityFunction(DiscreteDistribution( dom_IsList, prob_IsList), _x)_( Length(dom)=Length(prob) And Simplify(Add(prob))=1) <--
    [
      Local(i);
      i:=Find(dom,x);
      If(i = -1,0,prob[i]);
    ];
10 # ProbabilityDensityFunction( ChiSquareDistribution( _m),x_IsRationalOrNumber)_(x<=0) <-- 0;
20 # ProbabilityDensityFunction( ChiSquareDistribution( _m),_x) <-- x^(m/2-1)*Exp(-x/2)/2^(m/2)/Gamma(m/2);

10 # ProbabilityDensityFunction(tDistribution(_m),x) <-- Gamma((m+1)/2)*(1+x^2/m)^(-(m+1)/2)/Gamma(m/2)/Sqrt(Pi*m);

%/mathpiper



%mathpiper_docs,name="ProbabilityDensityFunction",categories="User Functions;Statistics & Probability"
*CMD ProbabilityDensityFunction --- probability density function
*STD
*CALL
	ProbabilityDensityFunction(dist,x)

*PARMS
{dist} -- a distribution type

{x} -- a value of random variable

*DESC
If {dist} is a discrete distribution, then {ProbabilityDensityFunction} returns the
probability for a random variable with distribution {dist} to take a
value of {x}. If {dist} is a continuous distribution, then {ProbabilityDensityFunction}
returns the density function at point $x$.

*SEE CumulativeDistributionFunction
%/mathpiper_docs