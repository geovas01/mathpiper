%mathpiper,def="ConfidenceIntervalOfTheMean",scope="private"

Retract("ConfidenceIntervalOfTheMean",*);

ConfidenceIntervalOfTheMean(sampleMean,standardDeviation,standardDeviationIsKnown,sampleSize,confidenceLevel) :=
[
    Check(IsBoolean(standardDeviationIsKnown),"The third argument must be True or False.");
    
    Local(criticalZScore,tValue,standardErrorOfTheMean,upperLimit,lowerLimit);
    
    If(sampleSize >= 30 Or standardDeviationIsKnown = True,
    [
        criticalZScore := N(ConfidenceLevelToZScore(confidenceLevel));

        standardErrorOfTheMean := N(StandardErrorOfTheMean(standardDeviation,sampleSize));
        
        upperLimit := N(sampleMean + criticalZScore * standardErrorOfTheMean);
        
        lowerLimit := N(sampleMean - criticalZScore * standardErrorOfTheMean);
        
        If(InVerboseMode(),
            [
                Echo("Using the normal distribution.");
                
                Echo("Critical Z-Score: ", criticalZScore);
                
                Echo("Standard error of the mean: ", standardErrorOfTheMean);
            ]);
    ],
    [   
        tValue := OneTailAlphaToTValue(sampleSize - 1, N((1 - confidenceLevel)/2));
        
        standardErrorOfTheMean := StandardErrorOfTheMean(standardDeviation,sampleSize);
        
        upperLimit := N(sampleMean + tValue * standardErrorOfTheMean);
        
        lowerLimit := N(sampleMean - tValue * standardErrorOfTheMean);
        
        If(InVerboseMode(),
            [
                Echo("Using the t-distribution.");
                
                Echo("t-value: ", tValue);
                
                Echo("Standard error of the mean: ", standardErrorOfTheMean);
            ]);
    
    ]);
    
    {lowerLimit,upperLimit};
];


%/mathpiper

    %output,preserve="false"
      Result: True
.   %/output





%mathpiper_docs,name="ConfidenceIntervalOfTheMean",categories="User Functions;Statistics & Probability"
*CMD ConfidenceIntervalOfTheMean --- calculates a confidence interval
*STD
*CALL
	ConfidenceIntervalOfTheMean(sampleMean,standardDeviation,standardDeviationIsKnown,sampleSize,confidenceLevel)
*PARMS
{sampleMean} -- the mean of the sample
{standardDeviation} -- the standard deviation of the sample
{standardDeviationIsKnown} -- True or False
{sampleSize} -- the size of the sample
{confidenceLevel} -- the desired confidence level

*DESC
This function calculates a confidence interval for a mean.  It returns a list which
contains the lower limit as its first element and the upper limit as
its second element.  If the sample size is <30 or {standardDeviationIsKnown} is False, then
Student's t-distribution is used during the calculation.

*E.G.
In> ConfidenceIntervalOfTheMean(78.25,37.50,True,32,.90)
Result: {67.34605578,89.15394422}
%/mathpiper_docs

    %output,preserve="false"
      
.   %/output


