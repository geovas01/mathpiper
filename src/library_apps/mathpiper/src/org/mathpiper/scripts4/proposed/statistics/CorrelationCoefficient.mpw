%mathpiper,def="CorrelationCoefficient",scope="private"

CorrelationCoefficient(x,y) :=
[   
    Check(IsList(x), "The first argument must be a list.");
    
    Check(IsList(y), "The second argument must be a list.");
    
    Check(Length(x) = Length(y), "The lists for argument 1 and argument 2 must have the same length.");
    
    Local(n);
        
    n := Length(x);
    
    N((n*Sum(x*y)-Sum(x)*Sum(y))/Sqrt((n*Sum(x^2)-(Sum(x))^2)*(n*Sum(y^2)-(Sum(y)^2))) );
];

%/mathpiper

    %output,preserve="false"
      Result: True
.   %/output




%mathpiper_docs,name="CorrelationCoefficient",categories="User Functions;Statistics & Probability"
*CMD CorrelationCoefficient --- calculates the correlation coefficient between two lists of values
*STD
*CALL
	CorrelationCoefficient(xList,yList)

*PARMS

{xList} -- the list of domain values
{yList} -- the list of range values

*DESC
This function calculates the correlation coefficient between two lists of values.

*E.G.
/%mathpiper

x := {4,3,5,2,3,4,3};
y := {83,86,92,78,82,95,80};

CorrelationCoefficient(x,y);

/%/mathpiper

    /%output,preserve="false"
      Result: 0.7766185090
.   /%/output

*SEE CorrelationMatrix
%/mathpiper_docs

    %output,preserve="false"
      
.   %/output


