%mathpiper,def="Remove"

Remove(list, expression) :=
{
   Local(result);
   Assign(result,[]);
   ForEach(item,list)
   Decide(item !=? expression, DestructiveAppend(result,item));
   result;
};

%/mathpiper



%mathpiper_docs,name="Remove",categories="Programming Functions;Lists (Operations)"
*CMD Remove --- remove all occurrences of an expression from a list
*STD
*CALL
        Remove(list, expr)

*PARMS

{list} -- list to act on

{expr} -- expression to look for in "list"

*DESC

This command removes all elements that match a given expression from a given list and returns the resulting list.

*E.G.
In> Remove([_a,_b,_a,_b,_c,_a,_c],_a)
Result> [_b,_b,_c,_c]

*SEE RemoveDuplicates

%/mathpiper_docs