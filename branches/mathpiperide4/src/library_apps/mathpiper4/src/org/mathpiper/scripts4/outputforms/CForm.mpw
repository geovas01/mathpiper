%mathpiper,def="CForm;CFormDoublePrecisionNumber;CFormArgs;CFormStatement"

/* CForm: convert MathPiper objects to C/C++ code. */

/* version 0.3 */

/* Changelog
        0.1        CForm() derived from TeXForm() v0.4. Have basic functionality. Do not allow list manipulation, unevaluated derivatives, set operations, limits, integrals, Infinity, explicit matrices. Complex numbers and expressions are handled just like real ones. Indexed symbols are assumed to be arrays and handled literally. No declarations or prototypes are supplied. Function definitions are not handled. Sum() is left as is (can be defined as a C function).
        0.2 Fix for extra parens in Sin() and other functions; fixes for Exp(), Abs() and inverse trig functions
        0.3 Fix for indexed expressions: support a[2][3][4]
        0.3.1 Fix for CForm(integer): add a decimal point
        0.4 Support While()[]. Added CFormable?. Use Concat() instead of Union() on lists.
        0.4.1 Support False, True
  0.4.2 Changed it so that integers are not coerced to floats any more automatically (one can coerce integers to floats manually nowadays by adding a decimal point to the string representation, eg. 1. instead of 1).
*/

/* To do:
        0. Find and fix bugs.
        1. Chop strings that are longer than 80 chars?
        2. Optimization of C code?
*/

RulebaseHoldArguments("CForm",[expression]);
RulebaseHoldArguments("CForm",[expression, precedence]);

Function ("CFormBracketIf", [predicate, string])
{
        Check(Boolean?(predicate) And? String?(string), "Argument", "CForm internal error: non-boolean and/or non-string argument of CFormBracketIf");
        If(predicate, ConcatStrings("( ", string, ") "), string);
};

CFormDoublePrecisionNumber(x_Number?) <--
{
    Local(i,n,s,f);
    s := ToString(x);
    n := Length(s);
    f := False;
    For(i := 1, i <=? n, i++)
    {
        If(s[i] =? "e" Or? s[i] =? ".", f := True);
    };
    If(f, s, s ~ ".");
};

/* Proceed just like TeXForm()
*/

// CFormMaxPrec should perhaps only be used from within this file, it is thus not in the .def file.
CFormMaxPrec() := 60000;         /* This precedence will never be bracketed. It is equal to KMaxPrec */

100 # CForm(_x) <-- CForm(x, CFormMaxPrec());

/* Replace numbers and variables -- never bracketed except explicitly */
110 # CForm(x_Integer?, _p) <-- ToString(x);
111 # CForm(x_Zero?, _p) <-- "0.";
112 # CForm(x_Number?, _p) <-- CFormDoublePrecisionNumber(x);
/* Variables are left as is, except some special ones */
190 # CForm(False, _p) <-- "false";
190 # CForm(True, _p) <-- "true";
200 # CForm(x_Atom?, _p) <-- ToString(x);

/* Strings must be quoted but not bracketed */
100 # CForm(x_String?, _p) <-- ConcatStrings("\"", x, "\"");

/* Replace operations */

/* arithmetic */

/* addition, subtraction, multiplication, all comparison and logical operations are "regular" */


LocalSymbols(cformRegularOps) {
  cformRegularOps := [ ["+"," + "], ["-"," - "], ["*"," * "],
                       ["/"," / "], [":="," = "], ["=="," == "],
                       ["=?"," == "], ["!=?"," != "], ["<=?"," <=? "],
                       [">=?"," >= "], ["<?"," < "], [">?"," > "],
                       ["And?"," && "], ["Or?"," || "], [">>", " >> "],
                       [ "<<", " << " ], [ "&", " & " ], [ "|", " | " ],
                       [ "%", " % " ], [ "^", " ^ " ],
                     ];

  CFormRegularOps() := cformRegularOps;
}; // LocalSymbols(cformRegularOps)

        /* This is the template for "regular" binary infix operators:
100 # CForm(_x + _y, _p) <-- CFormBracketIf(p<?PrecedenceGet("+"), ConcatStrings(CForm(x, LeftPrecedenceGet("+")), " + ", CForm(y, RightPrecedenceGet("+")) ) );
        */

        /* unary addition */
100 # CForm(+ _y, _p) <-- CFormBracketIf(p<?PrecedenceGet("+"), ConcatStrings(" + ", CForm(y, RightPrecedenceGet("+")) ) );

        /* unary subtraction */
100 # CForm(- _y, _p) <-- CFormBracketIf(p<?PrecedenceGet("-"), ConcatStrings(" - ", CForm(y, RightPrecedenceGet("-")) ) );

        /* power's argument is never bracketed but it must be put in braces. */
100 # CForm(_x ^ _y, _p) <-- CFormBracketIf(p<=?PrecedenceGet("^"), ConcatStrings("pow(", CForm(x, CFormMaxPrec()), ", ", CForm(y, CFormMaxPrec()), ")" ) );

100 # CForm(if(_pred)_body, _p) <-- "if ("~CForm(pred,60000)~") "~CForm(body);
100 # CForm(_left else _right, _p) <-- CForm(left)~" else "~CForm(right);


LocalSymbols(cformMathFunctions) {
  cformMathFunctions :=
    [
      ["Sqrt","sqrt"],
      ["Cos","cos"],
      ["Sin","sin"],
      ["Tan","tan"],
      ["Cosh","cosh"],
      ["Sinh","sinh"],
      ["Tanh","tanh"],
      ["Exp","exp"],
      ["Ln","log"],
      ["ArcCos","acos"],
      ["ArcSin","asin"],
      ["ArcTan","atan"],
      ["ArcCosh","acosh"],
      ["ArcSinh","asinh"],
      ["ArcTanh","atanh"],
      ["Maximum","max"],
      ["Minimum","min"],
      ["Abs","fabs"],
      ["Floor","floor"],
      ["Ceil","ceil"],
    ["!","factorial"]
    ];

  CFormMathFunctions() := cformMathFunctions;

}; // LocalSymbols(cformMathFunctions)

/* Precedence of 120 because we'd like to process some special functions like pow() first */


120 # CForm(expr_Function?, _p)_(ArgumentsCount(expr)=?2 And? Contains?(AssocIndices(CFormRegularOps()), Type(expr)) ) <--
      CFormBracketIf(p<?PrecedenceGet(Type(expr)), ConcatStrings(CForm(FunctionToList(expr)[2], LeftPrecedenceGet(Type(expr))), CFormRegularOps()[Type(expr)], CForm(FunctionToList(expr)[3], RightPrecedenceGet(Type(expr))) ) );


/* Sin, Cos, etc. and their argument is always bracketed */

120 # CForm(expr_Function?, _p) _
      (ArgumentsCount(expr)=?1 And? Contains?(AssocIndices(CFormMathFunctions()), Type(expr)) ) <--
      ConcatStrings(CFormMathFunctions()[Type(expr)], "(", CForm( FunctionToList(expr)[2], CFormMaxPrec()),")" );

/* functions */

/* Unknown function, precedence 200. Leave as is, never bracket the function itself and bracket the argumentPointer(s) automatically since it's a list. Other functions are precedence 100 */

CFormArgs(list_List?) <--
{
  Local(i,nr,result);
  result:="";
  nr:=Length(list);
  For (i:=1,i<=?nr,i++)
  {
    result:=result~CForm(list[i]);
    If (i<?nr, result:=result~", ");
  };
  result;
};


200 # CForm(_x, _p)_(Function?(x)) <--
{
  ConcatStrings(Type(x), "(", CFormArgs(Rest(FunctionToList(x))),")" );
};

/* Complex numbers */
100 # CForm(Complex(0, 1), _p) <-- "I";
100 # CForm(Complex(_x, 0), _p) <-- CForm(x, p);
110 # CForm(Complex(_x, 1), _p) <-- CForm(x+Hold(I), p);
110 # CForm(Complex(0, _y), _p) <-- CForm(Hold(I)*y, p);
120 # CForm(Complex(_x, _y), _p) <-- CForm(x+Hold(I)*y, p);

/* Some special functions: Mod */

100 # CForm(Modulo(_x, _y), _p) <-- CFormBracketIf(p<?PrecedenceGet("/"), ConcatStrings(CForm(x, PrecedenceGet("/")), " % ", CForm(y, PrecedenceGet("/")) ) )
;

/* Indexed expressions are never bracketed */
// the rule with [ ] seems to have no effect?
//100 # CForm(_x [ _i ], _p) <-- ConcatStrings(CForm(x, CFormMaxPrec()), "[", CForm(i, CFormMaxPrec()), "]");
100 # CForm(Nth(_x, _i), _p) <-- ConcatStrings(CForm(x, CFormMaxPrec()), "[", CForm(i, CFormMaxPrec()), "]");

LocalSymbols(cindent) {
  cindent:=1;

  NlIndented():=
  {
    Local(result);
// carriage return, so needs to start at the beginning of the line
    result:=
"
";
    Local(i);
    For(i:=1,i<?cindent,i++)
    {
      result:=result~"  ";
    };
    result;
  };
  CIndent() :=
  {
  (cindent++);
  "";
  };
  CUndent() :=
  {
  (cindent--);
  "";
  };
}; // LocalSymbols(cindent)

CFormStatement(_x) <-- CForm(x) ~ ";" ~ NlIndented();

120 # CForm(_x,_p)_(Type(x) =? "Block") <--
{
  Local(result);
  result:=CIndent()~"{"~NlIndented();
  ForEach(item,Rest(FunctionToList(x)))
  {
    result:=result~CFormStatement(item);
  };
  result:=result~"}"~CUndent()~NlIndented();
  result;
};

120 # CForm(For(_from,_to,_step)_body,_p) <--
  "for(" ~ CForm(from,CFormMaxPrec()) ~ ";"
        ~ CForm(to,CFormMaxPrec()) ~ ";"
        ~ CForm(step,CFormMaxPrec()) ~ ")"
        ~ CIndent() ~ NlIndented()
        ~ CFormStatement(body) ~ CUndent();

120 # CForm(While(_pred)_body, _p) <--
        "while(" ~ CForm(pred,CFormMaxPrec()) ~ ")"
        ~ CIndent() ~ NlIndented()
        ~ CFormStatement(body) ~ CUndent();

%/mathpiper



%mathpiper_docs,name="CForm",categories="User Functions;Input/Output"
*CMD CForm --- export expression to C++ code
*STD
*CALL
        CForm(expr)
        
*PARMS

{expr} -- expression to be exported

*DESC

{CForm} returns a string containing C++ code that attempts to implement the MathPiper expression {expr}. Currently the exporter handles most expression types but not all.

*E.G.

In> CForm(Sin(a1)+2*Cos(b1));
Result: "sin(a1) + 2 * cos(b1)";

*SEE PrettyForm, TeXForm, CFormable?
%/mathpiper_docs






%mathpiper,name="CForm",subtype="automatic_test"

Verify(
CForm(Hold(Cos(A-B)*Sin(a)*func(b,c,d*(e+Pi))*Sqrt(Abs(C)+D)-(g(a+b)^(c+d))^(c+d)))
,"cos(A - B) * sin(a) * func(b, c, d * ( e + Pi) ) * sqrt(fabs(C) + D) - pow(pow(g(a + b), c + d), c + d)"
);

Verify(
CForm(Hold({i:=0;While(i<?10){i++; a:=a+Floor(i);};}))
, "{
  i = 0;
  while(i <? 10)
    {
      ++(i);
      a = a + floor(i);
      }
    ;
    ;
  }
"
);

/* Check that we can still force numbers to be floats in stead of integers if we want to */
Verify(
CForm(Hold({i:=0.;While(i<?10.){i++; a:=a+Floor(i);};}))
, "{
  i = 0.;
  while(i <? 10.)
    {
      ++(i);
      a = a + floor(i);
      }
    ;
    ;
  }
"
);

%/mathpiper





%mathpiper,name="CForm",subtype="automatic_test"

/* Jitse's bug report, extended with the changes that do not coerce integers to floats automatically
   any more (just enter a dot and the number becomes float if that is what is intended).
 */
Verify(CForm(4), "4");
Verify(CForm(4.), "4.");
Verify(CForm(0), "0");
Verify(CForm(0.), "0.");

%/mathpiper