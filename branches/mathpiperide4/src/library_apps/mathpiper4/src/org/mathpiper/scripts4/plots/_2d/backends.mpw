%mathpiper,def="Plot2DOutputs;Plot2DData;Plot2DJava;ColorForGraphNr;Plot2DGeogebra;Plot2DJFreeChart"

//////////////////////////////////////////////////
/// Backends for 2D plotting
//////////////////////////////////////////////////


/// List of all defined backends and their symbolic labels.
/// Add any new backends here

LocalSymbols(options)
{
    options  := [
        ["default", "data"],
        ["data", "Plot2DData"],
        ["java", "Plot2DJava"],
    ["geogebra", "Plot2DGeoGebra"],
    ["jfreechart", "Plot2DJFreeChart"],
];
    

Plot2DOutputs() := options;

};

/*
        How backends work:
        Plot2D<backend>(values, optionsHash)
        optionsHash is a hash that contains all plotting options:
        ["xrange"] - a list of [x1, x2], ["xname"] - name of the variable to plot, ["yname"] - array of string representations of the function(s), and perhaps other options relevant to the particular backend.
        [values] is a list of lists of pairs of the form [[[x1, y1], [x2, y2], ...], [[x1, z1], [x2, z2], ...], ...] corresponding to the functions y(x), z(x), ... to be plotted. The abscissa points x[i] are not the same for all functions.
        The backend should prepare the graph of the function(s). The "datafile" backend Plot2Ddatafile(values, optionsHash) may be used to output all data to file(s), in which case the file name should be given by the value optionsHash["filename"]. Multiple files are created with names obtained by appending numbers to the filename.
        Note that the "data" backend does not do anything and simply returns the data.
        The backend Plot2Ddatafile takes care not to write "Infinity" or "Undefined" data points (it just ignores them). Custom backends should either use Plot2Ddatafile or take care of this themselves.
*/

/// trivial backend: return data list (do not confuse with Plot2Dgetdata() defined in the main code which is the middle-level plotting routine)
Plot2DData(values_List?, _optionsHash) <-- values;

/// The Java back-end generates a call-list that the Java graph plotter can handle
Plot2DJava(values_List?, _optionsHash) <--
{
  Local(result,count);
  count := 0;
  result:="$plot2d:";

  result := result~" pensize 2.0 ";
  ForEach(function,values)
  {
    result := result~ColorForGraphNr(count);
    count++;
    result:=result~" lines2d "~ToString(Length(function));

    function:=Select(function, Lambda([item],item[2] !=? Undefined));

    ForEach(item,function)
    {
      result := result~" "~ToString(item[1])~" "~ToString(item[2])~" ";
    };
  };
  WriteString(result~"$");
  True;
};

10 # ColorForGraphNr(0) <-- " pencolor 64 64 128 ";
10 # ColorForGraphNr(1) <-- " pencolor 128 64 64 ";
10 # ColorForGraphNr(2) <-- " pencolor 64 128 64 ";
20 # ColorForGraphNr(_count) <-- ColorForGraphNr(Modulo(count,3));




//GeoGebra backend.
Plot2DGeogebra(values_List?, _optionsHash) <--
{
  Local(result,count);
  count := 0;
  result:="";


  ForEach(function,values)
  {

    function:=Select(function, Lambda([item],item[2] !=? Undefined));

    ForEach(item,function)
    {
      result := result~"("~ToString(item[1])~","~ToString(item[2])~")"~Nl();
    };
  };
  WriteString(result);
  True;
};




//JFreeChart backend.
Plot2DJFreeChart(values_List?, _optionsHash) <--
{
    Local(rangeList, domainList, function, allProcessedFunctionData, lineChartCallListForm);
    
    
    
    //Remove Plot2Ds options so that they dont get passed through to LineChart();
    ForEach(name, ["xrange", "xname", "yname", "output", "precision", "points", "depth"])
    {
        AssociationDelete(optionsHash, name);
    };
    
    
    
    //Convert [x,y] pairs into [x,x,x,...] [y,y,y,...] form.
    allProcessedFunctionData := [];
    
    ForEach(function,values)
    {
        rangeList := [];
        
        domainList := [];
        
        function := Select(function, Lambda([item],item[2] !=? Undefined));
        
        ForEach(item,function)
        {
            rangeList := Append(rangeList, item[1]);
          
            domainList := Append(domainList, item[2]);
        };
        
        allProcessedFunctionData := Append(allProcessedFunctionData, rangeList);
        allProcessedFunctionData := Append(allProcessedFunctionData, domainList);
    
    };


    
    //Put LineChart() function call into list form so it can be manipulated.
    lineChartCallListForm := ['LineChart, allProcessedFunctionData ];
    
    
    
    //Add any options to the list.
    ForEach(key, AssociationIndices(optionsHash))
    {
        lineChartCallListForm := Append(lineChartCallListForm, Apply(":", [key, optionsHash[key]]));
    };
    
    
    
    //Call the LineChart() function.
    Eval(ListToFunction(lineChartCallListForm));
    

};



%/mathpiper

    %output,preserve="false"
      Result: True
.   %/output


