%mathpiper,def="Intersection"

10 # Intersection( LoL_List? )::(AllSatisfy?("List?",LoL)) <--
{
  //Decide(Verbose?(),Tell("Intersection_listOfLists",LoL));
  Local(nLists,L0,L1,ii,result,LI);
  nLists := Length(LoL);
  //Tell("  ",nLists);
  Decide( nLists =? 1,
    {
        result := LoL[1];
    },
    {
      L0 := FlatCopy(LoL[1]);
      For( ii:=2,ii<=?nLists,ii++)
      {
          L1 := FlatCopy(LoL[ii]);
          //Decide(Verbose?(),Tell("    ",[ii,L0,L1]));
          LI := Intersection(L0,L1);
          //Decide(Verbose?(),Tell("  -->",LI));
          L0 := FlatCopy(LI);
      };
      //Decide(Verbose?(),Tell("  result ",L0));
      result := L0;
    }
  );
  result;
};
  

11 # Intersection(list1_List?,list2_List?) <--
{
    //Decide(Verbose?(),Tell("Intersection_pairOfLists",[list1,list2]));
    Local(l2,index,result);
    l2:=FlatCopy(list2);
    result:=[];
    ForEach(item,list1)
    {
        Assign(index, Find(l2,item));
        Decide(index>?0,
          {
             DestructiveDelete(l2,index);
             DestructiveInsert(result,1,item);
          }
        );
    };
    DestructiveReverse(result);
};


%/mathpiper

    %output,preserve="false"
      Result: True
.   %/output




%mathpiper_docs,name="Intersection",categories="Programming Functions;Lists (Operations)"
*CMD Intersection --- return the Intersection of two or more lists
*STD
*CALL
        Intersection(L1, L2)
or
    Intersection( ListOfLists )

*PARMS

{L1}, {L2} -- two lists
or
{ListOfLists}  --  a List of two or more lists

*DESC

The Intersection of all the lists is determined and
returned. The Intersection contains all elements that occur in all
lists. The entries in the result are listed in the same order as in
the first list. If an expression occurs multiple times in all the
lists, then it will occur the same number of times in the result.

*E.G.

In> Intersection([_a,_b,_c], [_b,_c,_d])
Result: [_b,_c]

In> Intersection([_a,_e,_i,_o,_u], [_f,_o,_u,_r,_t,_e,_e,_n])
Result: [_e,_o,_u]

In> Intersection([1,2,2,3,3,3], [1,1,2,2,3,3])
Result: [1,2,2,3,3]

In> Intersection([[1,2,2,3,3,3,_a,_c,_e],[1,1,2,2,3,3,_a,_c,_e],[3,_a,_b,_c,_d,_e,_f,1,3,5]])
Result: [1,3,3,_a,_c,_e]

    
*SEE Union, Difference

%/mathpiper_docs
