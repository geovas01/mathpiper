%mathpiper,def="CanProve"

/* Small theorem prover for propositional logic, based on the
 * resolution principle.
 * Written by Ayal Pinkus, based on the simple theorem prover from "Prolog, Ivan Bratko, chapter 20"
 * Version 0.1 initial implementation.
 *
 *
 * Examples:
CanProve(( (a=>b) And (b=>c)=>(a=>c) ))  <-- True
CanProve(a  Or   Not a)                  <-- True
CanProve(True  Or  a)                    <-- True
CanProve(False  Or  a)                   <-- a
CanProve(a  And   Not a)                 <-- False
CanProve(a  Or b Or (a And b))           <-- a Or b
 */

 // <==> LogicSimplify(expr, 3)

/* CanProve tries to prove that the negation of the negation of
   the proposition is true. Negating twice is just a trick to
   allow all the simplification rules a la De Morgan to operate
 */
/*CanProve(_proposition)    <-- CanProveAux( Not CanProveAux( Not proposition));*/

CanProveAux(_proposition)    <-- LogicSimplify(proposition, 3);

CanProve(_proposition)      <-- CanProveAux( proposition );

%/mathpiper



%mathpiper_docs,name="CanProve",categories="User Functions;Propositional Logic"
*CMD CanProve --- try to prove statement
*STD
*CALL
	CanProve(proposition)

*PARMS

{proposition} -- an expression with logical operations

*DESC

MathPiper has a small built-in propositional logic theorem prover.
It can be invoked with a call to {CanProve}.

An example of a proposition is: "if a implies b and b implies c then
a implies c". MathPiper supports the following logical operations:

{Not} :     negation, read as "not"

{And}  :   conjunction, read as "and"

{Or}  :   disjunction, read as "or"

{=>}  : implication, read as "implies"

The abovementioned proposition would be represented by the following expression,

	( (a=>b) And (b=>c) ) => (a=>c)

MathPiper can prove that is correct by applying {CanProve}
to it:

In> CanProve(( (a=>b) And (b=>c) ) => (a=>c))
Result: True;

It does this in the following way: in order to prove a proposition $p$, it
suffices to prove that  $Not p$ is false. It continues to simplify  $Not p$
using the rules:

	Not  ( Not x)      --> x
(eliminate double negation),
	x=>y  -->  Not x  Or  y
(eliminate implication),
	Not (x And y)  -->  Not x  Or  Not y
(De Morgan's law),
	Not (x Or y) -->  Not x  And  Not y
(De Morgan's law),
	(x And y) Or z --> (x Or z) And (y Or z)
(distribution),
	x Or (y And z) --> (x Or y) And (x Or z)
(distribution),
and the obvious other rules, such as,
	True Or x --> True
etc.
The above rules will translate a proposition into a form

	(p1  Or  p2  Or  ...)  And  (q1  Or  q2
	   Or  ...)  And ...
If any of the clauses is false, the entire expression will be false.
In the next step, clauses are scanned for situations of the form:

	(p Or Y)  And  ( Not p Or Z) --> (Y Or Z)
If this combination {(Y Or Z)} is empty, it is false, and
thus the entire proposition is false.

As a last step, the algorithm negates the result again. This has the
added advantage of simplifying the expression further.

*E.G.

In> CanProve(a  Or   Not a)
Result: True;
In> CanProve(True  Or  a)
Result: True;
In> CanProve(False  Or  a)
Result: a;
In> CanProve(a  And   Not a)
Result: False;
In> CanProve(a  Or b Or (a And b))
Result: a Or b;
	

*SEE True, False, And, Or, Not
%/mathpiper_docs





%mathpiper,name="CanProve",subtype="automatic_test"


NextTest("Propositional logic theorem prover: CanProve");

Verify(CanProve(( (a=>b) And (b=>c) => (a=>c) )),True);
Verify(CanProve((((a=>b) And (b=>c))=> (a=>c) )),True);
Verify(CanProve(( (a=>b) And((b=>c) => (a=>c)))),((Not a Or b)And(Not a Or (b Or c))) );
//KnownFailure(BadOutput + WhenPreviousLine + IsUncommented);
//And *my* previous line (the KnownFailure) aborts.  (witnessed by no report from next line).
Verify(CanProve( True ),True);

Verify(CanProve(a Or Not a)                   ,True);
Verify(CanProve(True Or a)                 ,True);
Verify(CanProve(False Or a)                ,a   );
Verify(CanProve(a And Not a)                   ,False);
Verify(CanProve(a Or b Or (a And b))               ,a Or b  );

/* Two theorems from the Pure Implicational Calculus (PIC), in which the
 * only operator is [material] implication.  From the first, all other
 * theorems in PIC can be proved using only the two transformation rules:
 * 1. Rule of substitution.  Uniform replacement in theorems yields theorems.
 * 2. Rule of detachment, or modus ponens.  If 'a' and 'a=>b' are theorems, then 'b' is a theorem.
 *
 * 1. Lukasiewicz, Jan, "The Shortest Axiom of the Implicational Calculus
 *    of Propositions," Proceedings of the Royal Irish Academy, vol. 52,
 *    Sec. A, No. 3 (1948).  [ Can you say "Polish Notation"? ]
 * 2. Meredith, David, "On a Property of Certain Propositional Formulae,"
 *    Notre Dame Journal of Formal Logic, vol. XIV, No. 1, January 1973.
 */
Verify(CanProve(	/* 1. CCCpqrCCrpCsp */
     ((p=>q) => r) => ((r=>p) => (s=>p))
     ), True);
Verify(CanProve(	/* 2. CCpCqrCqCpr */
     (p => (q=>r)) => (q => (p=>r))
     ), True);


Verify(CanProve((A Or B) And Not A),            B And Not A);
Verify(CanProve((A Or B) And (Not A Or C)),     (A Or B) And (C Or Not A));
Verify(CanProve((B Or A) And (Not A Or C)),     (A Or B) And (C Or Not A));
Verify(CanProve( A And (A Or B Or C)),       A);
Verify(CanProve( A And (Not A Or B Or C)),  A And (B Or C));

// this is a test of contradication, A==3 should kick A==2 out as they're contradictory
Verify(CanProve( A==3 And (A==2 Or B Or C)),  A-3==0 And (B Or C));
//TODO Verify(CanProve( A==3 And (A<2  Or B Or C)),  A-3==0 And (B Or C));
//TODO Verify(CanProve( A==3 And (A>2  Or B Or C)),  (A-3==0) And (((A-2) > 0) Or B Or C));

Verify(CanProve(Not(Not (p_2-NULL==0))Or Not(p_2-NULL==0)), True);

LogicVerify(CanProve(P Or (Not P And Not Q)),P Or Not Q);

LogicVerify(CanProve(P Or (Not P And Not Q)),P Or Not Q);
LogicVerify(CanProve(A>0 And A<=0),False);
LogicVerify(CanProve(A>0 Or A<=0),True);

%/mathpiper
